ext {
    groupId = 'org.twitter4j'
    versionName = '4.0.8-20240310'
    versionDescription = 'Twitter4J Version ' + versionName
}

allprojects  {
    apply plugin: 'maven-publish'

    group = groupId
    version = versionName
}

// for build
// $ ./gradlew clean build -x test
subprojects {
    apply plugin: 'java'

    sourceCompatibility = 11
    targetCompatibility = 11

    [compileJava, compileTestJava, javadoc].each {
        it.options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://repo.maven.apache.org/maven2" }
    }
}

// Kotlin
buildscript {
    ext.kotlin_version = '1.9.21'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

////apply plugin: 'kotlin'
//dependencies {
//    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
//}
//compileKotlin {
//    kotlinOptions {
//        jvmTarget = "1.8"
//    }
//}
//compileTestKotlin {
//    kotlinOptions {
//        jvmTarget = "1.8"
//    }
//}

// for publish
// $ ./gradlew publishToMavenLocal  # deploy to ~/.m2/repository/org/twitter4j/
subprojects {
    apply plugin: 'maven-publish'

    javadoc {
        failOnError = false
    }

    // custom tasks for creating source/javadoc jars
    tasks.register('sourcesJar', Jar) {
        dependsOn classes
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    tasks.register('javadocJar', Jar) {
        dependsOn javadoc
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }

    // add javadoc/source jar tasks as artifacts
    artifacts {
        archives sourcesJar, javadocJar
    }

    publishing {
        publications {
            MyPublication(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                groupId groupId
                version versionName
            }
        }
        repositories {
            maven {
                url "file://${rootDir}/release_to_github_io/takke.github.io/maven/"
            }
        }
    }
}
