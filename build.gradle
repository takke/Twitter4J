ext {
    // local directory for deploy of results to upload to github
    repo = new File(rootDir, "repository")
    groupId = 'org.twitter4a'
    versionName = '4.1.1'
}

allprojects  {
    apply plugin: 'maven'

    group = groupId
    version = versionName
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

clean.doFirst {
    delete repo.absolutePath
}

// for build
subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    [compileJava, compileTestJava, javadoc].each {
        it.options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()
        maven { url "http://repo.maven.apache.org/maven2" }
    }
}

// for publish
subprojects {
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    // add javadoc/source jar tasks as artifacts
    artifacts {
        archives sourcesJar, javadocJar
    }

    publishing {
        publications {
            MyPublication(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                groupId groupId
                version versionName
            }
        }
    }

    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        // this usually comes from ~/.gradle/gradle.properties

        publications = ['MyPublication']
        override = false
        publish = true
        dryRun = false
        pkg {
            repo = 'maven'
            name = 'Twitter4A'
            userOrg = 'panecraft'
            desc = 'A Java library for the Twitter API'
            publicDownloadNumbers = true
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/takke/twitter4a.git'
            githubRepo = 'takke/twitter4a'

            version {
                name = versionName
                vcsTag = versionName
                released = new Date()
            }
        }
    }

}
