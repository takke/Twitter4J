ext {
    groupId = 'org.twitter4j'
    versionName = '4.0.8-20200820'
    versionDescription = 'Twitter4J Version ' + versionName
}

allprojects  {
    apply plugin: 'maven'

    group = groupId
    version = versionName
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}

// for build
// $ ./gradlew clean build -x test
subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    [compileJava, compileTestJava, javadoc].each {
        it.options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.maven.apache.org/maven2" }
    }
}

// for publish
// $ ./gradlew publishToMavenLocal  # deploy to ~/.m2/repository/org/twitter4j/
subprojects {
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    // add javadoc/source jar tasks as artifacts
    artifacts {
        archives sourcesJar, javadocJar
    }

    publishing {
        publications {
            MyPublication(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                groupId groupId
                version versionName
            }
        }
    }

    //--------------------------------------------------
    // upload to bintray
    //--------------------------------------------------
    // ./gradlew clean bintrayUpload -x test
    // and publish it on https://bintray.com/takke/maven/twitter4j-takke-mod
    bintray {
        user = hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

        // false => push upload button on bintray web page to distribute new version manually.
        publish = false

//        configurations = ['MyPublication']

        pkg {
            repo = 'maven'
            name = 'twitter4j-takke-mod'
            userOrg = user
            licenses = ['Apache-2.0']
            labels = ['java']
            vcsUrl = vcsUrl
            version {
                name = versionName
                desc = versionDescription
                vcsTag = versionName
            }
        }
    }

    bintrayUpload.doFirst {
        publications = publishing.publications.collect {
            it.name
        }
    }

    bintrayUpload.dependsOn publishToMavenLocal
}
